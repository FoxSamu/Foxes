/*
 * Copyright 2021 Shadew
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 *    limitations under the License.
 */

plugins {
    id 'fabric-loom' version '0.10-SNAPSHOT' apply false
    id 'maven-publish'
    id 'java'
}

subprojects {
    if (!project.name.contains("forge")) {
        apply plugin: 'fabric-loom'
        apply plugin: 'maven-publish'

        sourceCompatibility = JavaVersion.VERSION_17
        targetCompatibility = JavaVersion.VERSION_17

        archivesBaseName = rootProject.archives_base_name
        version = rootProject.mod_version + "+" + project.name
        group = rootProject.maven_group

        configurations {
            embed {
                transitive false
            }
        }

        afterEvaluate {
            jar {
                from configurations.embed.collect { it.isDirectory() ? it : zipTree(it) }
            }
        }

        dependencies {
            minecraft "com.mojang:minecraft:${rootProject.minecraft_version}"
            mappings loom.officialMojangMappings()
        }

        processResources {
            inputs.property "version", project.version

            filesMatching("fabric.mod.json") {
                expand "version": project.version
            }
        }

        tasks.withType(JavaCompile).configureEach {
            it.options.encoding = "UTF-8"

            it.options.release = 17
        }

        java {
            withSourcesJar()
        }

        jar {
            from("LICENSE") {
                rename { "${it}_${project.archivesBaseName}" }
            }
        }
    }
}

sourceCompatibility = JavaVersion.VERSION_16
targetCompatibility = JavaVersion.VERSION_16

archivesBaseName = rootProject.archives_base_name
version = rootProject.mod_version
group = rootProject.maven_group
